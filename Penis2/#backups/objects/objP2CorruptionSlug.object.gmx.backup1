// 2024-06-30 23:27:46
#event properties (no comments/etc. here are saved)
parent_index = -1;
sprite_index = sprP2BlankCentered;
persistent = true;
uses_physics = false;

#event create
tileset = tPenis2Tileset;
tileset_width = -1;

tile_depth = 1000000;
tile_index_max = 20;
tile_index_min = 4;
tile_index_shift = 4;
tile_size = 32;

tail_x = 0;
tail_y = 0;

lifetime = 0;
frequency = 1.0;
initialized = false;

visual_instance = noone;

movement_pattern_x = array(1, 1, 1);
movement_pattern_y = array(0, 0, 1);
movement_direction_x_norm = 0;
movement_direction_y_norm = 0;
movement_index = 0;
movement_pattern_length = 0;
movement_interval = (1/50) * (5.8 + random(1));
movement_timer = 0;

corruption_interval = (1/50) * 4;
corruption_timer = 0;
corruption_length = 4;

image_angle = 0;

roomwrap = true;

#event destroy
if (instance_exists(visual_instance))
    instance_destroy(visual_instance);

#event step

lifetime += (1/50);

movement_timer += (1/50);
if (movement_timer >= movement_interval && movement_interval > 0)
{
    movement_timer -= movement_interval;
    
    x += tile_size * movement_pattern_x[movement_index];
    y += tile_size * movement_pattern_y[movement_index];
    
    movement_index = (movement_index + 1) % movement_pattern_length;

    //checks for if slug head/tail is inside room, if not, destroy
    //head check & tail check
    if (!point_in_rectangle(x + 1, y + 1, 0, 0, room_width, room_height) &&
        !point_in_rectangle(tail_x + 1, tail_y + 1, 0, 0, room_width, room_height))
    {
        if (roomwrap)
        {
            x -= floor(x / room_width) * room_width;
            y -= floor(y / room_height) * room_height;
        }
        else
        {
            instance_destroy();
        }
    }
}

corruption_timer += (1/50);

if (corruption_timer >= corruption_interval)
{
    corruption_timer -= corruption_interval;
    
    var
    _tmx = x,
    _tmy = y,
    _mi = movement_index,
    _world = instance_find(oP2World, 0),
    _pipe_47_tileset = _world.pipe_47_tileset,
    _tx, _ty, _td, _prev_tile_type, _tile_type;
    
    for (var i = 0; i < corruption_length; i++)
    {
        _tx = floor(_tmx / tile_size);
        _ty = floor(_tmy / tile_size);
        _td = scrP2TilemapGetInRoom(_tx, _ty);
        
        if (_td != -1) {
            _prev_tile_type = scrP2MainTilesetGetTileType(_td[?"tile_index"]);
            _td[?"tile_index"] = ((_td[?"tile_index"] - tile_index_min + tile_index_shift) % (tile_index_max - tile_index_min)) + tile_index_min;
            scrP2TileRoomUpdateAt(_tx, _ty, _pipe_47_tileset);
            
            if (_prev_tile_type == P2_TILE_TYPE.HORIZONTAL) {
                scrP2TileRoomUpdateAt(_tx + 1, _ty, _pipe_47_tileset);
                scrP2TileRoomUpdateAt(_tx - 1, _ty, _pipe_47_tileset);
            } else if (_prev_tile_type == P2_TILE_TYPE.VERTICAL) {
                scrP2TileRoomUpdateAt(_tx, _ty + 1, _pipe_47_tileset);
                scrP2TileRoomUpdateAt(_tx, _ty - 1, _pipe_47_tileset);
            }
            
            _tile_type = scrP2MainTilesetGetTileType(_td[?"tile_index"]);
            if (_tile_type == P2_TILE_TYPE.HORIZONTAL) {
                scrP2TileRoomUpdateAt(_tx + 1, _ty, _pipe_47_tileset);
                scrP2TileRoomUpdateAt(_tx - 1, _ty, _pipe_47_tileset);
            } else if (_tile_type == P2_TILE_TYPE.VERTICAL) {
                scrP2TileRoomUpdateAt(_tx, _ty + 1, _pipe_47_tileset);
                scrP2TileRoomUpdateAt(_tx, _ty - 1, _pipe_47_tileset);
            }
        }
        
        if (i == corruption_length - 1)
        {
            tail_x = _tmx;
            tail_y = _tmy;
        }
        
        _mi--;
        if (_mi < 0)
            _mi += movement_pattern_length;
        
        _tmx -= movement_pattern_x[_mi] * tile_size;
        _tmy -= movement_pattern_y[_mi] * tile_size;
        
        _tmx -= floor(_tmx / room_width) * room_width * roomwrap;
        _tmy -= floor(_tmy / room_height) * room_height * roomwrap;
    }
}

#event other_room_end
if (!persistent) {
    instance_destroy();
}

#event other_user0|init
if (initialized) {
    exit;
}
initialized = true;

tileset_width = floor(background_get_width(tileset) / tile_size);
movement_pattern_length = array_length_1d(movement_pattern_x);
x = floor(x / tile_size) * tile_size;
y = floor(y / tile_size) * tile_size;

var _movement_finish_x = 0, _movement_finish_y = 0;
for (var i = array_length_1d(movement_pattern_x) - 1; i >= 0; i--) {
    _movement_finish_x += movement_pattern_x[i];
    _movement_finish_y += movement_pattern_y[i];
}
var _average_movement_distance = point_distance(0, 0, _movement_finish_x, _movement_finish_y) / array_length_1d(movement_pattern_x);

var _width = 0, _height = 0, _mov_pat_ind = 0;
for (var i = corruption_length - 1; i >= 0; i--) {
    _width += movement_pattern_x[_mov_pat_ind];
    _height += movement_pattern_y[_mov_pat_ind];
    _mov_pat_ind = (_mov_pat_ind + 1) % array_length_1d(movement_pattern_x);
}

var _ts05 = tile_size / 2;
visual_instance = instance_create(x + _ts05 - _width * _ts05, y + _ts05 - _height * _ts05, objP2CorruptionSlugVisualized);
visual_instance.direction = point_direction(0, 0, _movement_finish_x, _movement_finish_y);
visual_instance.image_angle = visual_instance.direction;
if (movement_interval != 0.0) {
    visual_instance.speed = (_average_movement_distance * tile_size) / (movement_interval * 50.0);
} else {
    visual_instance.speed = 0;
}
instance_set_width(visual_instance, point_distance(0, 0, _width, _height) * 32 + 64);
instance_set_height(visual_instance, 96);

visual_instance.wiggle_frequency = 0.6 + point_distance(0, 0, _width, _height) / 4 * 0.35;

#event draw don't draw


#event draw_end debug draw
/*
var _x = x, _y = y, _mi = movement_index;
draw_set_color(c_red);
for (var i = 0; i < corruption_length; i++) {
    draw_rectangle(_x, _y, _x + tile_size, _y + tile_size, true);
    
    _mi--;
    if (_mi < 0) {
        _mi += movement_pattern_length;
    }
    
    _x -= movement_pattern_x[_mi] * tile_size;
    _y -= movement_pattern_y[_mi] * tile_size;
}
draw_set_color(c_white);
*/