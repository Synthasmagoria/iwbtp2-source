// 2024-06-21 12:00:02
#event properties (no comments/etc. here are saved)
parent_index = -1;
sprite_index = sprSave_new;
mask_index = sprSaveMask;
uses_physics = false;

#event create
destroyDifficulty = 1;

image_speed = 0;
canSave = true;
grav = 1;       //sets which player gravity this save works with
warpID = 0;
warpXOffset = 0
warpYOffset = 0

angle=0
index=0
animate=false
imspeed=4/40

#event alarm0
canSave = true;

#event step
angle+=360/50
if angle>360 angle-=360

if !animate {
    if index > 0 {
        index -= imspeed
    }
} else {
    if index <4 index += imspeed else animate=false
}

#event collision:objPlayer
if (scrButtonCheckPressed(global.shootButton))  //save if player is touching the save and shooting
{
    event_user(0);
}

#event collision:objBullet
event_user(0);  //save the game
with other instance_destroy()

#event other_room_start
if (global.difficulty > destroyDifficulty)  //destroy if on a higher difficulty than hard
    instance_destroy();

#event other_animation_end
image_index = 0;
image_speed = 0;

#event other_user0|save the game

var _player = instance_find(objPlayer, 0);

// Make sure that the player isn't inside a tile in p2 when saving
if (_player && instance_exists(oP2World)) {
    var
    _l = floor(_player.bbox_left / 32),
    _t = floor(_player.bbox_top / 32),
    _r = floor(_player.bbox_right / 32),
    _b = floor(_player.bbox_bottom / 32);
    if (scrP2TilemapGetInRoom(_l, _t) != -1 ||
        scrP2TilemapGetInRoom(_r, _t) != -1 ||
        scrP2TilemapGetInRoom(_l, _b) != -1 ||
        scrP2TilemapGetInRoom(_r, _b) != -1) {
        exit;
    }
}

if (canSave && _player && global.grav == grav)
{
    if (!((_player.x < 0 || _player.x > room_width || _player.y < 0 || _player.y > room_height) && global.edgeDeath))  //make sure the player isn't saving outside the room to prevent save locking
    {
        //audio_play_sound(sndSavesound2,0,false);
        canSave = false;    //make it so that the player can't save again immediately
        alarm[0] = 30;  //set alarm so the player can save again
        
        with (instance_create(x+(sprite_width/2),y+(sprite_height/2),SaveChrist)) { image_xscale = other.image_xscale; image_yscale = other.image_yscale; }
        //with (instance_create(x+(sprite_width/2), y+(image_yscale*23), SaveRainbow)) { first = true; image_xscale *= other.image_xscale; image_yscale *= other.image_yscale; }
        animate = true
        
        scrSaveGame(true);
        
        scrP2CheckCompletedQuests();
    }
}

#event draw
draw_sprite_ext(sprite_index,1,x,y,image_xscale,image_yscale,image_angle,image_blend,image_alpha)
draw_sprite_ext(sprSaveRainbowBody,floor(index),x+(image_xscale*16),y+(image_yscale*23),image_xscale,image_yscale,image_angle+angle,c_white,image_alpha)
draw_self()